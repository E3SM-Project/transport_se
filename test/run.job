#!/bin/tcsh -f 
#PBS -l walltime=0:10:00
#PBS -j oe
#PBS -l nodes=35:ppn=4    
#PBS -A FY081407
########################################################################
#     
#
#   1.  Edit the script to put in the correct paths to the source code, run directory
#   2.  Create a CMAKE machine file for your platform
#       (see $HOMME/cmake/machineFiles for examples) 
#   3.  Run this script.  If there is no executable, it will attempt to 
#       run 'cmake' to configure the model.  This only needs to be done once
#   4.  It will then attempt to compile the model.  
#   5.  It will then attempt to run the model using 'mpirun -np $NCPU'
#   
#
#######################################################################
# user set parameters
#######################################################################
set HOMME = ~/codes/transport_se     # location of HOMME source code and test cases
set wdir = ~/scratch1/transport_se   # directory for building and running
set MACH = $HOMME/cmake/machineFiles/redsky.cmake # machine specific CMAKE settings:

set NCPU = 2                         # number of CPUS to use
set NTHREADS = 1                     # number of openMP threads

# set resolution
set nlev = 64     # number of vertical layers (dont change)
set NE = 8       # horizontal resolution   NE=8 is 4 degree (ultra-low-res).  NE=120 is 1/4 degree
set qsize = 4     # number of tracers.  Might be reduced for ACME v1




#######################################################################
# script derived parameters (dont change these)
#######################################################################
set input = $HOMME/test/           # this test case
set vcoord = $HOMME/test/vcoord    # location of vertical coordinate files
set bld = $wdir/bld
mkdir -p $bld
cd $bld
set exe = $bld/src/preqx/preqx


#######################################################################
# Configure the build (only do this once)
#######################################################################
if ( -f $exe  ) then
  echo "Found executable.  Assuming CMAKE already run"
else
  echo "running CMAKE to configure the model"
  rm -rf CMakeFiles CMakeCache.txt src
  cmake -C $MACH -DQSIZE_D=$qsize -DPREQX_PLEV=$nlev -DPREQX_NP=4  \
   -DPREQX_USE_PIO=TRUE         \
   -DBUILD_HOMME_SWDGX=FALSE                     \
   -DBUILD_HOMME_SWEQX=FALSE                     \
   $HOMME
  make -j4 clean
endif

#######################################################################
# compile the code
#######################################################################
echo "Compiling preqx:"
make -j4 preqx
if ($status) exit


# low res for debugging
if ( $NE == 8 ) then
   # 3.75 degree    
   set tstep = 1440  
   set tstep = 10
   set nu = 2e16
endif
if ( $NE == 30 ) then
   # 3.75 degree    
   set tstep = 300
   set nu = 1e15
endif
# ACME target resolution.  
if ( $NE == 120 ) then
   # 0.25 degree
   set tstep = 75
   set nu = 1e13
endif


#set nu = 0


# number of hours between calls to prim_printstate
set sfreq = 12
@ sfreq *= 3600
@ sfreq /= $tstep
if ( $sfreq == 0 ) set sfreq = 1

set sfreq = 20
set name = run

# create working directory
mkdir -p $wdir/$name/movies
set wdir = $wdir/$name
cd $wdir
cp -a $vcoord vcoord

# create the namelist (input file) based on parameters set above
cd $input
rm -f $wdir/explicit.nl
sed s/ne=.\*/ne=$NE/ explicit.nl.sed |\
sed s/statefreq.\*/statefreq=$sfreq/ |\
sed s/qsize.\*/qsize=$qsize/ |\
sed s/NThreads.\*/NThreads=$NTHREADS/ |\
sed s/nu_q.\*/nu_q=$nu/ | \
sed s/NU1/$nu/   > $wdir/input.nl




cd $wdir
setenv OMP_NUM_THREADS $NTHREADS
date
mpirun -np $NCPU  $exe  <  input.nl
date

exit

ncl ~/ncl/test.3-$rindex-56.lon-p-cross-section.ncl \
    'fnam="movies/asp_tracer1.nc"' 'pfmt="pdf"'
ncl ~/ncl/test.3-$rindex-56.lat-p-cross-section.ncl \
    'fnam="movies/asp_tracer1.nc"'   'pfmt="pdf"'
ncl ~/cjncl/test.3-0-56.lat-p-cross-section.linear_pressure.ncl \
  'fnam="movies/asp_tracer1.nc"' 'pfmt="pdf"' 'tracer="Q1"'





